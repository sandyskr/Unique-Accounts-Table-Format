string standalone.Unique_Accounts_Wise_All_Opportunities_Table_Format(Int deal_id, String currency, String curr_symbol)
{
	html = "";
	html = html + "<style>";
	html = html + "table{width:100%;border-collapse:collapse;font-family:Arial,Helvetica,sans-serif;font-size:10px;color:#000;}";
	html = html + "th{border:1px solid #ccc;padding:6px;text-align:left;background-color:#f2f2f2;font-weight:bold;}";
	html = html + "td{border:1px solid #ccc;padding:4px 6px;vertical-align:top;}";
	html = html + ".subtotal-row{font-weight:bold;background-color:#f9f9f9;}";
	html = html + ".account-total-row{font-weight:bold;background-color:#eeeeee;}";
	html = html + ".grand-total-row{font-weight:bold;background-color:#dddddd;font-size:12px;}";
	html = html + ".right{text-align:right;}";
	html = html + ".center{text-align:center;}";
	html = html + "</style>";
	html = html + "<table><thead><tr>";
	html = html + "<th>Account Name</th><th>Shipping Address</th><th>Product Category</th><th>Line Description</th>";
	html = html + "<th class='right'>List Price (" + currency + ")</th><th class='center'>Discount %</th><th class='right'>Qty</th>";
	html = html + "<th class='right'>Discount Amount (" + currency + ")</th><th class='right'>Extended Price (" + currency + ")</th>";
	html = html + "</tr></thead><tbody>";
	grandListPrice = 0.0;
	grandQty = 0.0;
	grandDiscAmt = 0.0;
	grandExtPrice = 0.0;
	child_deals = zoho.crm.getRelatedRecords("Child_Deals","Deals",deal_id);
	if(child_deals == null || child_deals.isEmpty())
	{
		return html + "</tbody></table>";
	}
	for each child in child_deals
	{
		account = child.get("Account_Name");
		if(account == null)
		{
			continue;
		}
		acc_id = account.get("id").toLong();
		acc_name = account.get("name");
		acc_rec = zoho.crm.getRecordById("Accounts",acc_id);
		ship_addr = ifnull(acc_rec.get("Shipping_Stree2"),"") + ", " + ifnull(acc_rec.get("Shipping_City"),"") + ", " + ifnull(acc_rec.get("Shipping_State"),"") + " " + ifnull(acc_rec.get("Shipping_Code"),"") + " " + ifnull(acc_rec.get("Shipping_Country"),"");
		accData = Map();
		catList = List();
		accListPrice = 0.0;
		accQty = 0.0;
		accDiscAmt = 0.0;
		accExtPrice = 0.0;
		totalAccRows = 0;
		quotes = zoho.crm.getRelatedRecords("Quotes","Accounts",acc_id);
		for each proposal in quotes
		{
			quoteResp = invokeurl
			[
				url :"https://www.zohoapis.com/crm/v8/Quotes/" + proposal.get("id")
				type :GET
				connection:"zcrm"
			];
			if(quoteResp.get("data") == null)
			{
				continue;
			}
			quoteRecord = quoteResp.get("data").get(0);
			quoteProducts = quoteRecord.get("Quoted_Items");
			for each productRow in quoteProducts
			{
				prodName = productRow.get("Product_Name");
				prodDetails = zoho.crm.getRecordById("Products",prodName.get("id").toLong());
				category = ifnull(prodDetails.get("Product_Category"),"Other");
				l_price = ifnull(productRow.get("List_Price"),0).toDecimal();
				qty = ifnull(productRow.get("Quantity"),0).toDecimal();
				disc_amt = ifnull(productRow.get("Discount"),0).toDecimal();
				ext_price = ifnull(productRow.get("Net_Total"),0).toDecimal();
				gross = l_price * qty;
				disc_perc = 0.0;
				if(gross > 0)
				{
					disc_perc = ((gross - ext_price) / gross * 100).round(2);
				}
				desc = ifnull(prodDetails.get("Description"),prodName.get("name")).replaceAll("\n"," ").replaceAll("\r"," ").replaceAll("'","&#39;");
				itemMap = Map();
				itemMap.put("desc",desc);
				itemMap.put("price",l_price);
				itemMap.put("qty",qty);
				itemMap.put("disc_p",disc_perc);
				itemMap.put("disc_amt",disc_amt);
				itemMap.put("total",ext_price);
				if(!accData.containsKey(category))
				{
					accData.put(category,List());
					catList.add(category);
				}
				tempItems = accData.get(category);
				tempItems.add(itemMap);
				accData.put(category,tempItems);
				totalAccRows = totalAccRows + 1;
			}
		}
		totalAccRows = totalAccRows + catList.size();
		firstAccRow = true;
		for each catKey in catList
		{
			items = accData.get(catKey);
			catRowCount = items.size() + 1;
			catListPrice = 0.0;
			catQty = 0.0;
			catDiscAmt = 0.0;
			catExtPrice = 0.0;
			firstCatRow = true;
			for each item in items
			{
				html = html + "<tr>";
				if(firstAccRow)
				{
					html = html + "<td rowspan='" + totalAccRows + "'>" + acc_name + "</td>";
					html = html + "<td rowspan='" + totalAccRows + "'>" + ship_addr + "</td>";
					firstAccRow = false;
				}
				if(firstCatRow)
				{
					html = html + "<td rowspan='" + catRowCount + "'>" + catKey + "</td>";
					firstCatRow = false;
				}
				html = html + "<td>" + item.get("desc") + "</td>";
				html = html + "<td class='right'>" + curr_symbol + " " + item.get("price") + "</td>";
				html = html + "<td class='center'>" + item.get("disc_p") + "%</td>";
				html = html + "<td class='right'>" + item.get("qty") + "</td>";
				html = html + "<td class='right'>" + curr_symbol + " " + item.get("disc_amt").round(2) + "</td>";
				html = html + "<td class='right'>" + curr_symbol + " " + item.get("total").round(2) + "</td>";
				html = html + "</tr>";
				catListPrice = catListPrice + item.get("price") * item.get("qty");
				catQty = catQty + item.get("qty");
				catDiscAmt = catDiscAmt + item.get("disc_amt");
				catExtPrice = catExtPrice + item.get("total");
			}
			html = html + "<tr class='subtotal-row'>";
			html = html + "<td>" + catKey + " Subtotal</td>";
			html = html + "<td class='right'>" + curr_symbol + " " + catListPrice.round(2) + "</td>";
			html = html + "<td></td>";
			html = html + "<td class='right'>" + catQty.round(2) + "</td>";
			html = html + "<td class='right'>" + curr_symbol + " " + catDiscAmt.round(2) + "</td>";
			html = html + "<td class='right'>" + curr_symbol + " " + catExtPrice.round(2) + "</td>";
			html = html + "</tr>";
			accListPrice = accListPrice + catListPrice;
			accQty = accQty + catQty;
			accDiscAmt = accDiscAmt + catDiscAmt;
			accExtPrice = accExtPrice + catExtPrice;
		}
		html = html + "<tr class='account-total-row'>";
		html = html + "<td colspan='4'>Account Subtotal (" + acc_name + ")</td>";
		html = html + "<td class='right'>" + curr_symbol + " " + accListPrice.round(2) + "</td>";
		html = html + "<td></td>";
		html = html + "<td class='right'>" + accQty.round(2) + "</td>";
		html = html + "<td class='right'>" + curr_symbol + " " + accDiscAmt.round(2) + "</td>";
		html = html + "<td class='right'>" + curr_symbol + " " + accExtPrice.round(2) + "</td>";
		html = html + "</tr>";
		grandListPrice = grandListPrice + accListPrice;
		grandQty = grandQty + accQty;
		grandDiscAmt = grandDiscAmt + accDiscAmt;
		grandExtPrice = grandExtPrice + accExtPrice;
		break;
	}
	html = html + "<tr class='grand-total-row'><td colspan='4'>GRAND TOTAL</td>";
	html = html + "<td class='right'>" + curr_symbol + " " + grandListPrice.round(2) + "</td><td></td>";
	html = html + "<td class='right'>" + grandQty.round(2) + "</td>";
	html = html + "<td class='right'>" + curr_symbol + " " + grandDiscAmt.round(2) + "</td>";
	html = html + "<td class='right'>" + curr_symbol + " " + grandExtPrice.round(2) + "</td></tr>";
	html = html + "</tbody></table>";
	html = html.replaceAll("\"", "");
	return html;
}